def utils, streams
node {
    checkout scm
    utils = load("utils.groovy")
    streams = load("streams.groovy")
    pod = readFile(file: "manifests/pod.yaml")
}

properties([
    pipelineTriggers([]),
    parameters([
      string(name: 'VERSION',
             description: 'Fedora CoreOS Build ID to test',
             defaultValue: '',
             trim: true),
      string(name: 'S3_STREAM_DIR',
             description: 'Fedora CoreOS S3 Stream Directory',
             defaultValue: '',
             trim: true)
    ])
])

currentBuild.description = "[${params.VERSION}] Running"

// substitute the right COSA image into the pod definition before spawning it
pod = pod.replace("COREOS_ASSEMBLER_IMAGE", "coreos-assembler:master")

podTemplate(cloud: 'openshift', label: 'coreos-assembler', yaml: pod, defaultContainer: 'jnlp') {
    node('coreos-assembler') { container('coreos-assembler') {
        def ami, ami_region
        def no_ami = false

        stage('Fetch Metadata') {
            utils.shwrap("""
            coreos-assembler buildprep s3://${params.S3_STREAM_DIR}/builds
            """)

            def basearch = utils.shwrap_capture("coreos-assembler basearch")
            def meta_json = "builds/${params.VERSION}/${basearch}/meta.json"
            def meta = readJSON file: meta_json
            if (meta.amis.size() > 0) {
                ami = meta['amis'][0]['hvm']
                ami_region = meta['amis'][0]['name']
            } else {
                no_ami = true
            }
        }

        // fail immediately if the build contained no AMIs
        if (no_ami) {
          currentBuild.result = 'FAILURE'
          return
        }

        stage('AWS Kola Run') {
          utils.shwrap("""
          kola run -p aws --aws-ami ${ami} --aws-region ${ami_region} -b fcos -j 10 || :
          tar -cf - _kola_temp/ | xz -c9 > _kola_temp.tar.xz
          """)
          archiveArtifacts "_kola_temp.tar.xz"
        }

        def report = readJSON file: "_kola_temp/aws-latest/reports/report.json"
        if (report["result"] != "PASS") {
          currentBuild.result = 'FAILURE'
          return
        }
    }}
}
